load("@aspect_bazel_lib//lib:expand_template.bzl", "expand_template")
load("@aspect_bazel_lib//lib:tar.bzl", "tar")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@container_structure_test//:defs.bzl", "container_structure_test")
load("//oci:defs.bzl", "oci_image")

tar(
    name = "app",
    srcs = ["test.bash"],
    transform = { package_name(): "." }
)

write_file(
    name = "labels_tmpl",
    out = "labels.txt.tmpl",
    content = [
        "org.opencontainers.image.version=BUILD_VERSION",
        "org.opencontainers.image.source=https://github.com/bazel-contrib/rules_oci",
    ],
)

# Use the value of --embed_label under --stamp, otherwise use a deterministic constant
# value to ensure cache hits for actions that depend on this.
expand_template(
    name = "labels",
    out = "labels.txt",
    stamp_substitutions = {"BUILD_VERSION": "{{BUILD_EMBED_LABEL}}"},
    substitutions = {"BUILD_VERSION": "0.0.0"},
    template = "labels_tmpl",
)

oci_image(
    name = "image",
    base = "@ubuntu",
    cmd = ["test.sh"],
    labels = ":labels",
    tars = [":app"],
)

container_structure_test(
    name = "test",
    configs = ["test.yaml"],
    image = ":image",
)

# Test again, using the macro-provided syntax sugar where labels is a dict.
oci_image(
    name = "image_labels_dict",
    base = "@ubuntu",
    cmd = ["test.sh"],
    labels = {
        "org.opencontainers.image.version": "0.0.0",
        "org.opencontainers.image.source": "https://github.com/bazel-contrib/rules_oci",
    },
    tars = [":app"],
)

container_structure_test(
    name = "test2",
    configs = ["test.yaml"],
    image = ":image_labels_dict",
)
